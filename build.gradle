plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

group = 'com.tasktracker'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // JUnit 5
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // MSSQL Server
    runtimeOnly 'com.microsoft.sqlserver:mssql-jdbc:12.4.2.jre11'
}

application {
    mainClass = 'com.tasktracker.Main'
}

test {
    useJUnitPlatform()

    // Test Output konfigurieren
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams true
    }

    ignoreFailures = false
    failFast = false

    // Jacoco für Test Coverage
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false

    }
}

// Task für vollständigen Build mit Tests und Coverage
task fullBuild{
    group = 'build'
    description = 'Clean build with test and coverage report'

    dependsOn clean, test, jacocoTestReport

    doLast {
        println "========================================="
        println "Build erfolgreich abgeschlossen"
        println "Test Coverage Report: build/reports/jacoco/html/index.html"
        println "Test Report: build/reports/tests/test/index.html"
        println "========================================="
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.tasktracker.Main',
                'Implementation-Title': 'TaskTracker',
                'Implementation-Version': version,
                'Created-By': 'TDD Übung GSE2'
        )
    }

    from {
        configurations.runtimeClasspath.collect {
            it.isDirectory() ? it : zipTree(it).matching {
                exclude 'META-INF/*.RSA'
                exclude 'META-INF/*.SF'
                exclude 'META-INF/*.DSA'
                exclude 'META-INF/*.MF'
                exclude 'META-INF/LICENSE'
                exclude 'META-INF/NOTICE'
            }
        }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveBaseName = 'task-tracker'
    archiveVersion = ''
}

tasks.register('cc'){
    dependsOn clean
    doLast {
        println "Projekt bereinigt"
    }
}

tasks.register('tc'){
    dependsOn test
    doLast {
        println "Tests ausgeführt"
    }
}

tasks.register('cov'){
    dependsOn jacocoTestReport
    doLast {
        println "Coverage Report generiert"
    }
}