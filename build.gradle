plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

group = 'com.tasktracker'
version = '1.0-SNAPSHOT'
sourceCompatibility = '23'

repositories {
    mavenCentral()
}

dependencies {
    // JUnit 5
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // MSSQL Server
    implementation 'com.microsoft.sqlserver:mssql-jdbc:12.4.2.jre11'
}

application {
    mainClass = 'com.tasktracker.Main'
}

test {
    useJUnitPlatform()

    // Test Output konfigurieren
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams true
    }

    ignoreFailures = false
    failFast = false

    // Jacoco f체r Test Coverage
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        csv.required = false

    }
}

// Task f체r vollst채ndigen Build mit Tests und Coverage
task fullBuild{
    group = 'build'
    description = 'Clean build with test and coverage report'

    dependsOn clean, test, jacocoTestReport

    doLast {
        println "========================================="
        println "Build erfolgreich abgeschlossen"
        println "Test Coverage Report: build/reports/jacoco/html/index.html"
        println "Test Report: build/reports/tests/test/index.html"
        println "========================================="
    }
}

jar {
    manifest {
        attributes(
                'Main-Class': 'com.tasktracker.Main',
                'Implementation-Title': 'TaskTracker',
                'Implementation-Version': version,
                'Created-By': 'myenituerk',
                'Build-Timestamp': new java.text.SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss.SSSZ").format(new Date())
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveBaseName = 'task-tracker'
    archiveVersion = ''
}

tasks.register('cc'){
    dependsOn clean
    doLast {
        println "Projekt bereinigt"
    }
}

tasks.register('tc'){
    dependsOn test
    doLast {
        println "Tests ausgef체hrt"
    }
}

tasks.register('cov'){
    dependsOn jacocoTestReport
    doLast {
        println "Coverage Report generiert"
    }
}